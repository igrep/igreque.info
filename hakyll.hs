{-# LANGUAGE OverloadedStrings #-}
module Main where

import Data.Monoid ((<>))
import Control.Applicative ((<$>))

import Hakyll

main :: IO ()
main = hakyll $ do
  cssRules         -- Compress CSS
  postRules        -- Render posts
  postsListRules   -- Render posts list
  indexRules       -- Index
  taggedPostsRules -- display posts tagged as a praticular tag
  atomRules        -- render an Atom feed

cssRules :: Rules ()
cssRules =
  match "css/*" $ do
    route   idRoute
    compile compressCssCompiler

postRules :: Rules ()
postRules =
  match "posts/*" $ do
    route   $ setExtension ".html"
    compile $ do
      tags <- getMyTags
      let loadWithTags = loadTemplateIn (taggedCtx tags)
      pandocCompiler
        >>= loadWithTags "templates/post.html"
        >>= loadWithTags "templates/default.html"
        >>= relativizeUrls

postsListRules :: Rules ()
postsListRules = do
  create ["posts.html"] $ do
    route idRoute
    compile $ do
      posts <- recentFirst <$> loadAll "posts/*"
      itemTpl <- loadBody "templates/postitem.html"
      list <- applyTemplateList itemTpl postCtx posts
      makeItem list
        >>= loadWithAllPosts "templates/posts.html"
        >>= loadWithAllPosts "templates/default.html"
        >>= relativizeUrls
  where
    loadWithAllPosts = loadTemplateIn allPostsCtx
    allPostsCtx = constField "title" "All posts" <> postCtx

indexRules :: Rules ()
indexRules = do
  create ["index.html"] $ do
    route idRoute
    compile $ do
      posts <- recentFirst <$> loadAll "posts/*"
      itemTpl <- loadBody "templates/postitem.html"
      list <- applyTemplateList itemTpl postCtx posts
      tags <- getMyTags
      makeItem list
        >>= loadAndApplyTemplate "templates/index.html" (indexCtx tags list)
        >>= loadAndApplyTemplate "templates/default.html" (indexCtx tags list)
        >>= relativizeUrls
      where
        indexCtx t l =
          constField "posts" l
          <> field "tagcloud" (\_ -> renderTagCloud 90 120 t)
          <> defaultContext

taggedPostsRules :: Rules ()
taggedPostsRules = do
  route idRoute
  tags <- getMyTags
  tagsRules tags $ \tag pattern -> do
    let title = "Posts tagged " ++ tag
    compile $ do
      list <- postList tags pattern recentFirst
      makeItem ""
        >>= loadAndApplyTemplate "templates/posts.html"
            ( constField "title" title
              <> constField "body" list
              <> defaultContext )
        >>= loadAndApplyTemplate "templates/default.html"
            ( constField "title" title
              <> defaultContext )
        >>= relativizeUrls

atomRules :: Rules ()
atomRules =
  create ["atom.xml"] $ do
    route idRoute
    compile $ do
      posts <- take 10 . recentFirst <$> loadAll "posts/*"
      renderAtom feedConfig feedCtx posts
  where
    feedCtx = bodyField "description" <> postCtx
    feedConfig = FeedConfiguration
      { feedTitle       = "igreque :: Info -> RSS"
      , feedDescription = "RSS feed of Igreque.info, generated by Hakyll."
      , feedAuthorName  = "Yuji Yamamoto"
      , feedAuthorEmail = "whosekiteneverfly@gmail.com"
      , feedRoot        = "http://igreque.info"
      }

getMyTags :: MonadMetadata m => m Tags
getMyTags = buildTags "posts/*" (fromCapture "tags/*.html")

taggedCtx :: Tags -> Context String
taggedCtx tags = tagsField "prettytags" tags <> postCtx

loadTemplateIn :: Context a -> Identifier -> Item a -> Compiler (Item String)
loadTemplateIn = flip loadAndApplyTemplate

postCtx :: Context String
postCtx = dateField "date" "%B %e, %Y" <> defaultContext

-- any better concrete name?
postList ::
  Tags -> Pattern -> ([Item String] -> [Item String])
    -> Compiler String
postList tags pattern preprocessor = do
    postItemTpl <- loadBody "templates/postitem.html"
    posts <- preprocessor <$> loadAll pattern
    applyTemplateList postItemTpl (taggedCtx tags) posts
