{-# LANGUAGE OverloadedStrings #-}
module Main where

import Data.Monoid ((<>))
import Control.Applicative ((<$>))

import Debug.Trace
import Hakyll

-- general function for debugging
inspect :: Show a => String -> a -> a
inspect msg x = trace ( msg ++ ": " ++ show x ) x
--

inspectTags :: Show a => a -> a
inspectTags = inspect "tags"

main :: IO ()
main = hakyll $ do
  tags <- buildTags "posts/*" (fromCapture "tags/*.html")
  cssRules              -- Compressed CSS
  idRules "fonts/*"     -- Fonts
  idRules "imgs/*"      -- Images
  postRules tags        -- Render posts
  postsListRules        -- Render posts list
  indexRules tags       -- Index
  taggedPostsRules tags -- Display posts tagged as a praticular tag
  atomRules             -- Atom feed
  templateRules         -- Template
  -- For slides written in HTML
  idRules "slides/*.html"
  idRules "slides/styles/*.css"
  idRules "slides/scripts/*.js"

idRules :: Pattern -> Rules ()
idRules pat =
  match pat $ do
    route idRoute
    compile copyFileCompiler

templateRules :: Rules ()
templateRules = match "templates/*" $ compile templateCompiler

cssRules :: Rules ()
cssRules =
  match "css/*" $ do
    route   idRoute
    compile compressCssCompiler

postRules :: Tags -> Rules ()
postRules tags =
  match "posts/*" $ do
    route   $ setExtension ".html"
    compile $ do
      let loadWithTags = loadTemplateIn (taggedContext tags)
      pandocCompiler
        >>= loadWithTags "templates/post.html"
        >>= saveSnapshot "content"
        >>= loadWithTags "templates/default.html"
        >>= relativizeUrls

postsListRules :: Rules ()
postsListRules = do
  create ["posts.html"] $ do
    route idRoute
    compile $ do
      posts <- recentFirst =<< loadAll "posts/*"
      itemTpl <- loadBody "templates/postitem.html"
      list <- applyTemplateList itemTpl datedContext posts
      makeItem list
        >>= loadWithAllPosts "templates/posts.html"
        >>= loadWithAllPosts "templates/default.html"
        >>= relativizeUrls
  where
    loadWithAllPosts = loadTemplateIn allPostsContext
    allPostsContext = constField "title" "All posts" <> datedContext

indexRules :: Tags -> Rules ()
indexRules tags = do
  create ["index.html"] $ do
    route idRoute
    compile $ do
      posts <- recentFirst =<< loadAll "posts/*"
      itemTpl <- loadBody "templates/postitem.html"
      list <- applyTemplateList itemTpl datedContext posts
      makeItem list
        >>= loadAndApplyTemplate "templates/index.html" (indexContext tags)
        >>= loadAndApplyTemplate "templates/default.html" defaultContext
        >>= relativizeUrls
      where
        indexContext t =
          field "tagcloud" (\_ -> renderTagCloud 90 120 t)
          <> defaultContext

taggedPostsRules :: Tags -> Rules ()
taggedPostsRules tags = do
  tagsRules tags $ \tag pattern -> do
    route idRoute
    let title = "Posts tagged " ++ tag
    compile $ do
      list <- postList tags pattern recentFirst
      makeItem ""
        >>= loadAndApplyTemplate "templates/posts.html"
            ( constField "title" title
              <> constField "body" list
              <> defaultContext )
        >>= loadAndApplyTemplate "templates/default.html"
            ( constField "title" title
              <> defaultContext )
        >>= relativizeUrls

atomRules :: Rules ()
atomRules =
  create ["atom.xml"] $ do
    route idRoute
    compile $ do
      posts <- take 10 <$> (recentFirst =<< loadAllSnapshots "posts/*" "content")
      renderAtom feedConfig feedContext posts
  where
    feedContext = bodyField "description" <> datedContext
    feedConfig = FeedConfiguration
      { feedTitle       = "igreque :: Info -> RSS"
      , feedDescription = "RSS feed of igreque :: Info, generated by Hakyll."
      , feedAuthorName  = "Yuji Yamamoto"
      , feedAuthorEmail = "whosekiteneverfly@gmail.com"
      , feedRoot        = "http://the.igreque.info"
      }

taggedContext :: Tags -> Context String
taggedContext tags = tagsField "prettytags" tags <> datedContext

loadTemplateIn :: Context a -> Identifier -> Item a -> Compiler (Item String)
loadTemplateIn = flip loadAndApplyTemplate

datedContext :: Context String
datedContext = dateField "date" "%B %e, %Y" <> defaultContext

-- any better concrete name?
postList ::
  Tags -> Pattern -> ([Item String] -> Compiler [Item String])
    -> Compiler String
postList tags pattern preprocessor = do
    postItemTpl <- loadBody "templates/postitem.html"
    posts <- preprocessor =<< loadAll pattern
    applyTemplateList postItemTpl (taggedContext tags) posts
