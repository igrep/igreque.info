---
title: Haskellアプリ作りのためのMonad Overview
author: Yuji Yamamoto
date: May, 2016
tags: Haskell, Monad
...
---

唐突ながら、会社でHaskellを採用する可能性が高まりつつあるので、
チームのメンバーに向けて、Haskellで実践的なアプリを作る上で欠かせない、
Monadの使い方やその実用的な役割にフォーカスした概要を、できるだけ簡潔に説明したいと思う。  
実際には会社で使いながら補足していき、必要に応じてこの記事にも書き足す方向で。

# この文章を読んで知って頂きたいこと

- HaskellにおけるMonadの役割
- MonadがなぜHaskellにおいて必要なのか
- MaybeやState、IOなど、具体的なMonadの簡単な使い方
    - （できれば）それらを使ったアプリやライブラリの実装例、実装パターン

# この文章を読んでも知ることができないこと

- Monadの定義
- Monadとは何なのか
- Monad則とは何なのか
- do記法やMonadの細かい仕組み
- などなど

# 予めおことわり

あくまでも使い方や役割を**感じ取ってもらう**ためのものなので、Monadの定義や実際の構造と照らしあわせていささか不正確な点があることをご了承願いたい。  
幸い、そうした誤解によって間違えたとしても、恐らくコンパイルエラーだけで済むだろうから。

# HaskellにおけるMonadの役割

いわゆる「純粋関数型プログラミング言語」などと知られている通り、Haskellは原則として、
プログラムを全て「純粋な関数」 --- 値を受け取って、返すことしかできない --- で構成するよう設計されている。  
しかしHaskellを敬遠する多くの人の想像するように、
実際のところ、「純粋な関数」だけでは冗長すぎて書きにくい場面も多い。  
例えば、同じ型の変数に関数をいくつも適用したいが、変数名をいくつもつけるのが面倒な場合や、それから、
感じ方の問題ではあるが、設計者が考えたモデルについて、どう考えても状態を持ったものとして表現したほうが適切だと考えられる場合など。  
あと、最もありふれたケースとして、入出力処理をしたい場合も該当する。

定義や理論はともかく、実践上、HaskellのMonadはそうしたニーズをごく自然な書き方で解決するための手段を提供する。  
具体的には、「値を受け取って、返すことしかできない」「純粋な関数」(`a -> b`のような型の関数)の戻り値の型に、
関数の「**権限を表すラベル**」を貼り付けることで、

「この関数は『値を受け取って、返すこと』**以外のこと**もしうるよ！」

ということを型で明示する、と考えるとわかりやすいだろう（し、私自身そのように考えながら関数の型を見る）。  

例えば、関数の型が下記のように異なっていた場合で説明しよう。

```haskell
pureFunction  :: a -> b
stateFunction :: a -> State s b
```

`pureFunction`が「`a`という型の値を受け取って、`b`という型の値を返すことしかできない」「純粋な関数」であるのに対して、
`stateFunction`は、「`a`という型の値を受け取って、`b`という型の値を返しつつ」、
「`s`という型の（とある一つの）変数を更新することもできるよ！」ということを明示している。

Haskellでは関数の型にそうした「権限」を明記し、制限することで、
プログラマは関数の与える影響範囲を簡単に知ることができるし、コンパイラにそれを保証させることができる。

これ以降で使う予定の用語を一点補足しておこう。  
先ほどの`stateFunction`の`b`にくっついた`State s`ような、
「**権限を表すラベル**」がくっついた型の値(`stateFunction`で言えば`State s b`)を、Haskellでは「アクション」だとか「Monadの値」と呼ぶ。  
そして`stateFunction`のような型の関数が返す値(`State s b`という型の値)を「`State` Monadのアクション」と呼ぶ。  
Haskellの型で一般化すると次のようになる。

```haskell
action :: Monad m => m b
```

# 各種Monadひとっ飛び

具体的な使い方は後に回して、ひとまずHaskellの世界にはどんなMonadがあるのか、
そしてそれらが「値を受け取って、返すこと」以外に何ができるか、ざっとリストアップする。  
知っているものを網羅させるが、私の独断と偏見により、重要度に応じて★をつけた。
とりあえずは、★を2つ以上つけたものの使い方だけでも理解していただきたい。

- State Monad (★):
    - `do`ブロックの中で共有している、とある型の変数を**更新**する。
    - `do`ブロックについては後述。
- **Writer Monad** (★★):
    - `do`ブロックの中で共有している、とある型の変数の値に**追記**する。
    - 例えば後述する`IO`を使わずに、ログをログらしく書きたい時などに使う。
- Reader Monad (★):
    - `do`ブロックの中で共有している、とある型の変数を**読む**。
    - 実質普通の関数と変わらないように聞こえる通り、実は普通の関数。
    - アプリケーションの広い範囲で共有する、書き換えない変数を自然に表現するために使う。
        - 外部から読んできた環境設定を表現したり、ダイナミックスコープな定数をシミュレートしたりするのに使う。
- **Maybe Monad** (★★):
    - 関数の実行が失敗した場合に、囲っている`do`ブロックから脱出する。
    - 他の言語でよく使う、「`return null;`などして関数の実行を途中で切り上げるイディオム」のベターな代わりとして使ったりする。
- **Either Monad** (★★):
    - 関数の実行が失敗した場合に、囲っている`do`ブロックから脱出するとともに、失敗した理由を伝える。
    - `throw`を使って、囲っている`try`ブロックを中断する機能の代わりに使う。
- List Monad (★):
    - 関数が返した複数の結果を、`do`ブロックの中で総当りで実行する。
    - 例えば他の言語でリストに対する`for`文がたくさんネストしてしまうケースにおいて、ネストをなくすのに使えたりする。
- Cont Monad:
    - 継続Monad。他の言語で言う`break`や`continue`をエミュレートするのに使ったりする。
    - [こういった使い方もある](http://qiita.com/tanakh/items/81fc1a0d9ae0af3865cb)。
    - 直接使うことは稀だと思うのでむやみに理解する必要はない。というか、私も仕組みを理解していない。
- **IO Monad** (★★★):
    - おなじみ入出力処理に加え、C言語で定義した関数の呼び出し、`IORef`という特殊な型の変数の破壊的変更、例外の送出など、だいたいなんでもできる。
    - 一言で言うとワイルドカードのようなMonad。
    - 必要以上に使えば使うほど、Haskellで書くメリットが減っていくのでご利用は計画的に。
- ST Monad:
    - `STRef`という特殊な型の変数の更新をする。
    - イミュータブルなデータ構造として作ると効率が悪い、配列などを操作したい場合に使う。
- Identity Monad:
    - 何もしない。後述するMonad Transformerから、通常のMonadを簡単に作るのに使う。
    - そのMonad Transformerを組み合わせて新しいMonadを作ろうとでも思わない限り、直接使うケースはないだろう。

- Free Monad:
    - Functorという型クラスを実装した型から、新たなMonadを簡単に作り出すための特殊なMonad。
- Operational Monad:
    - Free Monadを応用して、Functorを実装した型じゃなくても簡単に新しいMonadを作れるすごい特殊なMonad。

- **Monad Transformerと呼ばれるもの全般** (★★):
    - ここまでに挙げた、各種Monadが「値を受け取って、返す以外にできること」を、**同時に複数**使用できるようにする仕組み。
    - 大きめのアプリケーションにおいてしばしばあるケースなので、これも使い方だけは抑えたい。

- Monad\*という名前がついた型クラスに属する各種Monad (★):
    - 例えばMonadIO、MonadState、MonadWriter、MonadBaseControlなど。
    - ここまでに並べたとおり、Monadもいろいろあるので、似たようなものがそうした型クラスでまとめられている。
    - 「値を受け取って、返す以外に、hogehogeができるMonad」の「hogehoge」ごとに型クラスとして抽象化している。
- その他
    - これまでに挙げた様々なMonadを、各アプリケーションやライブラリ専用に組み合わせたものが多い。
        - あるいは、実装は独自だが本質的に他のMonadと似たものか。
    - 後はSTMやEvalなど、並列処理や並行処理用に作られたやつか。
        - こちらについて詳しくは<a href="http://www.amazon.co.jp/gp/product/4873116899/ref=as_li_ss_tl?ie=UTF8&amp;camp=247&amp;creative=7399&amp;creativeASIN=4873116899&amp;linkCode=as2&amp;tag=poe02-22">Haskellによる並列・並行プログラミング</a><img src="http://ir-jp.amazon-adsystem.com/e/ir?t=poe02-22&amp;l=as2&amp;o=9&amp;a=4873116899" width="1" height="1" border="0" alt="" style="border:none !important; margin:0px !important;" />を参照されたし。

# Monadの一般的な使い方

一部には`do`記法禁止、なんていう規約を設けているところもあるらしいが、
通例Monadは「`do`記法」という特別な構文糖（シンタックスシュガー）を介して使用すると、わかりやすく直感的に書けることが多い。  
そのため、以下にMonad全般（もちろん、ここまでに紹介したものを含む）で使用できる、「`do`記法」という構文糖の使い方を、
ある意味最も身近なMonadであろう、IO Monadを例に示す。

IOには、例えば下記のような型のアクション（と、アクションを返す関数）がある。

```haskell
-- 標準入力から文字列を一行読み出し、返すアクション
getLine :: IO String

-- 第一引数としてファイル名受け取り、その中身を全て読み出し、返すアクション
readFile :: String -> IO String
```

これらを`do`記法で使用する際は、下記のように`do`という予約語で始まるブロックの中に書く。

```haskell
do
  line <- getLine
  readFile line
```

`do`と書かれた行からインデントを一段下げた行までを「`do`ブロック」と言う。

`do`ブロックはその中で実行しているアクションの型の値を返す。
`do`ブロックは値を返すので、当然ながら変数に代入することができる。  
したがって下記の`largerAction`の型は`IO String`となる。

```haskell
largerAction :: IO String
largerAction = do
  line <- getLine
  readFile line
```

`do`ブロックが返す値は、`do`ブロックの中に列挙したアクションを「続けて実行する」アクションとなる。  
なので上記の`do`ブロックは、「`getLine`してから`readFile`する」アクションを返す。

`do`ブロックの中では、`<-`という記号を利用して、アクションの「結果」を変数に代入することができる。  
アクションの「結果」とは、アクションを「実際に実行した結果」を表す。  
そしてその型はアクションから「権限を表すラベル」を**剥がした**型となる。  
上記の例で言うと、`line <- getLine`という行が該当する。  
`line`には`getLine`を実行した結果、すなわち「標準入力から文字列を一行読みだした結果の文字列」が代入され、
その型は`IO String`から`IO`を剥がした型`String`となる。

`<-`で代入した変数は、もちろん`do`ブロックの中で他の関数に渡すことができる。  
なので当然ながら上記の`readFile`は`line`を引数として受け取ることができる。

`do`ブロックの最後の行に書かれたアクションが、`do`ブロック全体の最終的な型となる。  
やはりその型は最後の行に書かれたアクションの型がそのまま引き継がれる。  
上記の例で言うと`readFile :: String -> IO String`に引数を一つ適用した結果、つまり`IO String`となる。

まとめると上記のアクションは、「`getLine`した結果を`readFile`して、その結果を返す」アクション、
日本語に訳すと「標準入力から一行分読んだ文字列をファイル名として、その中身を読んで返す」アクションである。

`do`ブロックの中でアクションでない値、すなわち「値を受け取って、返すことしかできない」、
純粋な関数で作った値を作りたくなる場合も、当然あるだろう。  
そのような場合は`let`を使う。

例として、先程まで使った例を少し改造しよう。  
ファイル名を`getLine`で受け取ってから、
[MissingHというパッケージのData.String.Utilsモジュールにある、strip関数](https://hackage.haskell.org/package/MissingH-1.3.0.1/docs/Data-String-Utils.html)を使用して、
ファイル名の前後に含まれてしまった空白文字を取り除き、更に拡張子を加えてみよう。

```haskell
import Data.String.Utils (strip)

largerAction :: IO String
largerAction = do
  line <- getLine
  let path = strip line
      prefixedPath = path ++ ".dat"
      -- 通常のHaskellのletと異なり "in" は省略できる点に注意。
  readFile prefixedPath
```

<!--
```haskell
main :: IO ()
main = largerAction >>= print
```
-->

上記の通り`let`で代入した変数は後続のアクションや、`let`式の中で参照することができる。

## `do`ブロックの中でやってはいけない（できない）こと

`do`ブロックのなかで以下のことを行うと、コンパイルエラーになる。  
重要なものから順番に列挙しよう。

### その1: 同じ`do`ブロックで列挙するアクションの「権限を表すラベル」は、必ず一貫していなければならない。

例1: 下記の`largerAction`は、`IO String`という型なので、「権限を表すラベル」は`IO`である。  
一方、[`lookup`関数](http://haddock.stackage.org/lts-5.16/containers-0.5.6.2/Data-Map-Lazy.html#v:lookup)（連想配列 `Map`から指定したキーの値を取り出す関数）は、
下記の場合`Maybe [Char]`返すので、「権限を表すラベル」は`Maybe`である。  
したがって`IO`であることと一貫せず、コンパイルエラーとなる。

```haskell
import Prelude hiding (lookup)
import Data.Map (lookup, fromList)

countryCurrency =
  fromList [("USA", "Dollar"), ("France", "Euro")]

largerAction :: IO String
largerAction = do
  -- ↓でコンパイルエラー
  currency <- lookup "USA" countryCurrency
  readFile currency
```

エラー内容:

```haskell
    Couldn't match expected type ‘IO FilePath’
                with actual type ‘Maybe [Char]’
    In a stmt of a 'do' block: currency <- lookup "USA" countryCurrency
    In the expression:
      do { currency <- lookup "USA" countryCurrency;
           readFile currency }
```

例2: 同様に、下記の`largerAction`は、`IO String`という型なので、「権限を表すラベル」は`IO`である。  
一方、[`Control.Monad.Writer`の、`tell`関数](http://haddock.stackage.org/lts-5.16/mtl-2.2.1/Control-Monad-Writer-Lazy.html#v:tell)（[`mtl`](https://www.stackage.org/lts-5.16/package/mtl-2.2.1)というパッケージに入っている）は、
下記の場合`Writer String ()`を返すので、「権限を表すラベル」は`Writer [Char]`である [^monadWriter]。  
したがって`IO`であることと一貫せず、コンパイルエラーとなる。

[^monadWriter]: 実際には型クラスで抽象化されているので、厳密に言うと`(Monad m) => MonadWriter [Char] m`となる。

```haskell
import Control.Monad.Writer

largerAction :: IO String
largerAction = do
  -- ↓でコンパイルエラー
  tell "log"
  readFile "log"
```

エラー内容:

```haskell
    No instance for (MonadWriter [Char] IO)
      arising from a use of ‘tell’
    In a stmt of a 'do' block: tell "log"
    In the expression:
      do { tell "log";
           readFile "log" }
    In an equation for ‘largerAction’:
        largerAction
          = do { tell "log";
                 readFile "log" }
```

少しややこしいのだが、一点重要な補足を。  
同じ`do`ブロックの中では同じMonadしか使ってはいけない。これは事実なのだが、これを回避する方法がある。
`do`ブロック以外の方法で「権限を表すラベル」を剥がせばよいのだ。
先ほどの例1で言えば、例えば[fromMaybe](https://hackage.haskell.org/package/base-4.8.2.0/docs/Data-Maybe.html#v:fromMaybe)関数を利用すると、
`Maybe [Char]`の「権限を表すラベル」を剥がすことができる。

```haskell
import Prelude hiding (lookup)
import Data.Map (lookup, fromList)
import Data.Maybe (fromMaybe)

countryCurrency =
  fromList [("USA", "Dollar"), ("France", "Euro")]

largerAction :: IO String
largerAction = do
  --                       ↓はデフォルト値。lookupがNothingを返した時に使う。
  let currency = fromMaybe "JPY" $ lookup "USA" countryCurrency
  readFile currency
```

### その2: `do`ブロックの最後の行の値は、アクション（「権限を表すラベル」がくっついた型の値）でなければならない。

例1: 何度も同じ例からで恐縮だが、下記の`largerAction`は、`IO String`という型なので、
`do`ブロックの最後の行が返す値は、`IO String`でなければならない。  
しかし、下記では最後の行(`strip line`)が返す値は`String`と、権限を表すラベル(`IO`)がつけられた値ではない。
したがって`IO`であることと一貫せず、コンパイルエラーとなる。

```haskell
import Data.String.Utils (strip)

largerAction :: IO String
largerAction = do
  line <- getLine
  -- ↓でコンパイルエラー
  strip line
```

エラー内容:

```haskell
    Couldn't match type ‘[Char]’ with ‘IO String’
    Expected type: IO String
      Actual type: String
    In a stmt of a 'do' block: strip line
    In the expression:
      do { line <- getLine;
           strip line }
```

直すには、`return`という、全てのMonadで使える「権限を表すラベルを**貼り付けるだけで何もしない**関数」を使う。

```haskell
import Data.String.Utils (strip)

largerAction :: IO String
largerAction = do
  line <- getLine
  return $ strip line
```

あるいは、全てのMonadが`Functor`のインスタンスであることを利用して、次のように`<$>`を使っても良い。

```haskell
import Data.String.Utils (strip)

largerAction :: IO String
largerAction = do
  strip <$> getLine
```

このように書くと、`line`という一時的な変数を作らなくて済むので、より簡潔に書くことができる。  
Monadを使うだけでは必要ではないが、このスタイルに興味がある場合[Applicativeのススメ](http://d.hatena.ne.jp/kazu-yamamoto/20101211/1292021817)をご一読願いたい。  
ちなみにありがたいことに、GHCのバージョンが7.10となった今では、この記事で言うところの「理想化された世界」になっている。

### その3: `do`ブロックの最後の行では、`<-`を使った代入をしてはならない。

その2と関連した間違いとして、下記のような間違いをしてしまうかもしれない。

例:

```haskell
largerAction :: IO String
largerAction = do
  line <- getLine
  -- ↓でコンパイルエラー
  content <- readFile line
```

エラー内容:

```haskell
    The last statement in a 'do' block must be an expression
      content <- readFile line
```

特別深い理由はないのだが、上記のような状況では変数`content`を使うはずもないので、
おかしな`do`ブロックとみなされ、エラーになる。

直すには当然のごとく`content <-`の部分を消し、代入文でなくせば良い。

```haskell
largerAction :: IO String
largerAction = do
  line <- getLine
  readFile line
```

# 各種Monadの使い方つまみ食い

## IO Monad (★★★):

Haskellでアプリケーションを作る上で最も重要なMonadは`IO`である。  
というか、これを使わなければHaskellでアプリケーションを書くことは絶対にできない。  
これは「純粋な関数だけじゃプログラムが書けないんじゃないの？」というよくある疑念が実は真実であることと理由は同じだ。  
一応、理論的には`IO`のアクション（を返す関数）はあくまでも「アクション」を返しているだけなので純粋なのであるが、
少なくとも見かけ上、`IO`を使ったコードは手続き型スタイルに見えて仕方がないだろうし、そう捉えたほうが説明しやすいときのほうが多い。

前置きが長くなったが、とは言え、純粋に使うだけであれば`IO`は単純だ。  
普通のMonadと同じように、`do`ブロックの中に`IO`のアクションを返す関数を列挙していけば良い。

```haskell
greet :: IO ()
greet = do
  putStr "What's your name? "
  name <- getLine
  putStrLn $ "Hello, " ++ name ++ "!"
```

上記のように作られた`IO`のアクションは、`main`関数という大きな`IO`アクションの中で評価されて、初めて実際に実行される。  
`main`関数はC言語などでお馴染みの`main`関数と同じようなものだ。

```haskell
main :: IO ()
main = do
  if True
    then greet
    else putStrLn "I don't wanna greet at you!"
```

上記の場合、`if`式の条件部が`True`となっているので、
当然`then`節のみ`greet`のみが必ず実行され、`"I don't wanna greet at you!"`と表示されることはない。  
他の言語でも同じように振る舞うだろうし、特に違和感を感じることもないんじゃないかと思う。

アプリケーションを作る上で最も重要なMonadであるとともに、HaskellのMonadの中で最も変わった性質を持っているのも`IO`である。  
`IO`のアクションは他のMonadと異なり、「権限を表すラベル」を「純粋な関数」の中で剥がすことができないようになっているのである  
（ただし、[`unsafePerformIO`](http://haddock.stackage.org/lts-5.17/base-4.8.2.0/System-IO-Unsafe.html#v:unsafePerformIO)という、デバッグ用に作られた例外はある）。  
この性質は、「純粋な関数」と`IO`（入出力とか例外を投げるとか）を明確に区別するのに非常に役に立つ。コンパイラがそれを保証してくれるからだ。

## Maybe Monad (★★):

`Maybe` Monadはいわゆる「nullチェック」の煩わしさを劇的に緩和する。  
`Maybe`をMonadとして、すなわち`do`ブロックの中で使用した場合、
`do`ブロックの各行に並べたアクションが、1つでも失敗したら(`Nothing`を返したら)`do`ブロックから抜け出すことができる。

典型的な使用例は、複数の連想配列 `Map`から[lookup関数](http://haddock.stackage.org/lts-5.16/containers-0.5.6.2/Data-Map-Lazy.html#v:lookup)で値を取り出したいが、
そのうち一度でも値が見つからなかった場合は(`Nothing`を返して)関数の実行を終了させたい、なんて場合だろう。

```haskell
-- http://haddock.stackage.org/lts-5.16/containers-0.5.6.2/Data-Map-Lazy.html#v:lookup に書いてあった例から拝借。

import Prelude hiding (lookup)
import Data.Map (lookup, fromList)

-- 従業員の氏名をキーに、所属する部署名を持つMap
employeeDept =
  fromList [("John", "Sales"), ("Bob", "IT")]

-- 部署名をキーに、部署がある国名を持つMap
deptCountry =
  fromList [("IT", "USA"), ("Sales", "France")]

-- 国名をキーに、通貨の名前を持つMap
countryCurrency =
  fromList [("USA", "Dollar"), ("France", "Euro")]

-- 氏名から、（恐らく、給料を支払う際の）通過を調べる関数
employeeCurrency :: String -> Maybe String
employeeCurrency name = do
  -- ↓ここか、
  dept <- lookup name employeeDept
  -- ↓ここか、
  country <- lookup dept deptCountry
  -- ↓ ここのうち一つでもNothingを返したら、
  --    employeeCurrencyの実行は終了。Nothingが返る。
  lookup country countryCurrency
```

上記を愚直に`case`式を使ってチェックすると、次のようにやけにネストの深いコードとなってしまう。  
コメントを除けば行数が2倍以上になっているのにもお気づきだろうか？

```haskell
employeeCurrency name =
  case lookup name employeeDept of
    Just dept ->
      case lookup dept deptCountry of
        Just country ->
          lookup country countryCurrency
        Nothing -> Nothing
    Nothing -> Nothing
```

## Either Monad (★★):

`Either` Monadは`Maybe` Monadと同様に、アクションが失敗した際（`Nothing`と異なりアクションが`Left`を返した場合）に`do`ブロックから即座に抜け出す機能を持つ。  
`Either` Monadはそれに加えて、アクションが失敗した理由を`Left <エラーの理由を表す値>`として返すことができる。

「失敗した理由を加える事ができる」ということで、`Maybe` Monadの例に手を加えてみよう。  
[errorsというパッケージにある、`note`という関数](http://haddock.stackage.org/lts-5.17/errors-2.1.2/Control-Error-Util.html#v:note)を使えば、
Maybe Monadのアクションが失敗した時(`Nothing`を返した時)、付け加えたエラーメッセージに変換することができる。

```haskell
import Control.Error.Util (note)

employeeCurrency :: String -> Either String String
employeeCurrency name = do
  dept <- note "Department not found!" $ lookup name employeeDept
  country <- note "Country not found!" $ lookup dept deptCountry
  note "Currency not found!" $ lookup country countryCurrency
```

失敗した場合は、例えば次のようなエラーを返す。

```haskell
> employeeCurrency "Yuji"
Left "Currency not found!"
```

それから、`Either` Monadには他の言語にもよくある`throw`や`catch`のようなユーティリティ関数も用意されている。  
（使用するには[`mtl`パッケージに入っている`Control.Monad.Except`などのmodule](https://hackage.haskell.org/package/mtl-2.2.1/docs/Control-Monad-Except.html)が必要）

```haskell
> import Control.Monad.Except (throwError, catchError)

--                    残念ながら型注釈がないとエラーになる。
> throwError "Error!" :: Either String a
Left "Error!"
```

`catchError`は、バッククォートで2引数関数を中置演算子として使える機能を利用することで、
より他の言語の`catch`らしく使えるようになっている。

```haskell
> let errorCode = -1

> throwError "Error!" `catchError` (\_error -> return errorCode) :: Either String Int
Right (-1)
```

これらは後述するMonad Transformerの一部として使用すると、特にありがたみが感じられるだろう。

## Writer Monad (★★):

`Writer` Monadを正しく理解するには`Monoid`を理解する必要があるのだが、ここでは思い切って単純化して、
「`do`ブロックの中で共有している、**リストに追記**する」ものとして説明しよう（実際には`Monoid`型クラスさえ実装していれば、リスト以外でも良い）。  
`do`ブロックの中で`tell :: [w] -> Writer [w] ()`という関数を使用することによって、対象の`do`ブロックで共有しているリストに追記することができる。

例としては、`Either`ではエラーが起きた際に**直ちに`do`ブロックをぬけ出す**ものであったものに対して、
`Writer`では**エラーを記録するだけで、実行をやめない**例にしよう。  
フォームのバリデーションなどがそうしたケースに該当する。

```haskell
import Control.Monad.Writer (Writer, tell, runWriter)
import Data.Char (isDigit)

data UserInput =
  UserInput
    { name :: String
    , emailAddress :: String
    , annualIncome :: String
    }

validateInput :: UserInput -> Writer [String] ()
validateInput input = do
  mustBePresent "氏名" $ name input
  mustContainAtSign "メールアドレス" $ emailAddress input
  mustBePositiveInteger "年収" $ annualIncome input

mustBePresent :: String -> String -> Writer [String] ()
mustBePresent name text = do
  if null text
    then tell [name ++ "は必須入力項目です。"]
    else return ()

mustContainAtSign :: String -> String -> Writer [String] ()
mustContainAtSign name text = do
  if elem '@' text
    then return ()
    else tell [name ++ "の形式が正しくありません。"]

mustBePositiveInteger :: String -> String -> Writer [String] ()
mustBePositiveInteger name text = do
  if all isDigit
    then return ()
    else tell [name ++ "は整数で入力してください。"]
```

上記の例では、各種`must*`な関数において、入力された値が正しい値かどうかを確認し、そうでなければ`tell`でエラーメッセージを追記する。
さて、`tell`で追記したエラーメッセージは、どのようにして取り出すのだろうか。  
方法はいくつかあるが、最もオーソドックスな方法は、`runWriter`という関数を使うことだろう。  
`runWriter`の型は(この場合)`runWriter :: Writer [String] () -> ((), [String])`である。  
`Writer`の1つ目の型引数、すなわち`do`ブロックの中で共有している`[String]`を、「権限を表すラベル」を剥がした値として取り出すことができる。  
上の例の場合、例えば下記のような結果となる。

```haskell
> let input = UserInput "" "a" "nondigit"
> runWriter $ validateInput input
((),["\27663\21517\12399\24517\38920\20837\21147\38917\30446\12391\12377\12290","\12513\12540\12523\12450\12489\12524\12473\12398\24418\24335\12364\27491\12375\12367\12354\12426\12414\12379\12435\12290","\24180\21454\12399\25972\25968\12391\20837\21147\12375\12390\12367\12384\12373\12356\12290"])
```

おっと、日本語がエスケープ文字列に置き換えられてしまった。こういう時は[unicode-show](https://hackage.haskell.org/package/unicode-show)というパッケージを使おう。

```haskell
> import Text.Show.Unicode (uprint)
> uprint $ runWriter $ validateInput input
((),["氏名は必須入力項目です。","メールアドレスの形式が正しくありません。","年収は整数で入力してください。"])
```

## Monad Transformerと呼ばれるもの (★★):

これまでに述べたように、Monadはその種類（型）によって、「値を受け取って、返す以外にできること」が大きく異なる。  
そのため「値を受け取って、返す以外にできること」を、**同じ`do`ブロックの中で、複数同時に使いたい**場合も当然あるだろう。  
例えば`Maybe`によって「関数が失敗したら囲っている`do`ブロックから脱出」しつつ、`Writer`でログの追記も行いたいといった場合だ。  
Monad Transformerはそうしたニーズを満たすために存在する。

例のごとく詳しい仕組みは割愛するが、ここでは[お馴染みstack](http://docs.haskellstack.org/en/stable/README/)のソースを参考にしつつ、
よくありそうなCLIアプリをMonad Transformerで作る例を紹介しよう（実際のstackのMonad Transformerよりはかなり単純化する）。  
Monad Transformerを実装したパッケージはいくつか存在するが、ここでは一番有名な[mtl](https://hackage.haskell.org/package/mtl-2.2.1)を使用する。

最初に`Writer` Monadを使用して、ユーザーの入力から、成績をカウントするMonadを紹介しよう。

```haskell
```

http://qiita.com/7shi/items/4408b76624067c17e933
http://bicycle1885.hatenablog.com/entry/2012/12/08/165236
https://karczmarczuk.users.greyc.fr/matrs/Fuprog/Doc/transformers.pdf

## Monad\*という名前がついた型クラスに属する各種Monad (★):

Monad Transformerと関連が深い。


## Reader Monad (★):

## State Monad (★):

## Reader Monad (★):

## List Monad (★):
